// ----------------------------------
// RSDK Project: Sonic 1/Sonic 2
// Script Description: BG Animation Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias 1 : TYPE_BGANIMATION

private alias object.value0 : BGAnimation.BGTimer
private alias object.propertyValue : BGAnimation.enabled

// Static Values
public value BGAnimation_value0 = 0;

private alias BGAnimation_value0 : BGAnimation_currentPreview

event ObjectMain
	BGAnimation.BGTimer++
	BGAnimation.BGTimer &= 511
	Sin(temp0, BGAnimation.BGTimer)
	if temp0 > 0
		if temp0 < 32
			screen.yoffset = 256
		else
			screen.yoffset = 0
		end if
	else
		if temp0 > -32
			screen.yoffset = 512
		else
			screen.yoffset = 768
		end if
	end if
end event


event ObjectDraw
	if BGAnimation.enabled == 0
		// This script doesn't only do BG animation, it also deals with drawing the zone preview at the bottom right of the screen
		// BGAnimation_currentPreview is set by the Menu Control object
		temp0 = screen.xcenter
		temp0 -= 180
		if temp0 > 0
			temp0 = 0
		end if
		temp0 += screen.xcenter
		DrawSpriteScreenXY(14, temp0, 198)
		DrawSpriteScreenXY(BGAnimation_currentPreview, temp0, 198)
	end if
end event


event ObjectStartup

	LoadSpriteSheet("LevelSelect/Icons.gif")

	// Set all BG Animation objects to active
	// (There should only ever be one in a scene, though)
	foreach (TypeName[BG Animation], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].priority = PRIORITY_ACTIVE
	next

	// 0-13 - Zone previews
	SpriteFrame(56, -12, 32, 24, 0, 50) 	// 0  - Sonic heaaset gaming music thing
	SpriteFrame(56, -12, 32, 24, 1, 85)		// 1  - EHZ
	SpriteFrame(56, -12, 32, 24, 34, 85)	// 2  - CPZ
	SpriteFrame(56, -12, 32, 24, 67, 85)	// 3  - ARZ
	SpriteFrame(56, -12, 32, 24, 100, 85)	// 4  - CNZ
	SpriteFrame(56, -12, 32, 24, 133, 85)	// 5  - HTZ
	SpriteFrame(56, -12, 32, 24, 166, 85)	// 6  - MCZ
	SpriteFrame(56, -12, 32, 24, 199, 85)	// 7  - HPZ
	SpriteFrame(56, -12, 32, 24, 180, 1)	// 8  - OOZ
	SpriteFrame(56, -12, 32, 24, 213, 1)	// 9  - MTZ
	SpriteFrame(56, -12, 32, 24, 180, 26)	// 10 - SCZ
	SpriteFrame(56, -12, 32, 24, 213, 26)	// 11 - WFZ
	SpriteFrame(56, -12, 32, 24, 180, 51)	// 12 - DEZ
	SpriteFrame(56, -12, 32, 24, 213, 51)	// 13 - Special Stage

	// 14 - Selection frame
	SpriteFrame(32, -24, 80, 50, 0, 0)

	// Default to the EHZ icon
	BGAnimation_currentPreview = 1

end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event
